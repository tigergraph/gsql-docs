= Data Types
:prewrap:

This section describes the data types that are native to and are supported by the GSQL Query Language. Most of the data objects used in queries come from one of three sources:

* The query's input parameters
* The vertices, edges, and their attributes which are encountered when traversing the graph
* The variables defined within the query to assist in the computational work of the query

This section covers the following subset of the EBNF language definitions:

.EBNF for Data Types
[source,ebnf]
----
lowercase          := [a-z]
uppercase          := [A-Z]
letter             := lowercase | uppercase
digit              := [0-9]
integer            := ["-"]digit+
real               := ["-"]("."digit+) | ["-"](digit+"."digit*)
numeric            := integer | real
stringLiteral      := '"' [~["] | '\\' ('"' | '\\')]* '"'

name := (letter | "_") [letter | digit | "_"]*   // Can be a single "_" or start with "_"
graphName := name
queryName := name
paramName := name
vertexType := name
edgeType := name
accumName := name
vertexSetName := name
attrName := name
varName := name
tupleType := name
fieldName :=name
funcName := name

type := baseType | tupleType | accumType | STRING COMPRESS

baseType := INT
          | UINT
          | FLOAT
          | DOUBLE
          | STRING
          | BOOL
          | VERTEX ["<" vertexType ">"]
          | EDGE
          | JSONOBJECT
          | JSONARRAY
          | DATETIME

filePath := paramName | stringLiteral

typedef := TYPEDEF TUPLE "<" tupleFields ">" tupleType

tupleFields := (baseType fieldName) | (fieldName baseType)
           ["," (baseType fieldName) | (fieldName baseType)]*

parameterType := baseType
               | [ SET | BAG ] "<" baseType ">"
               | FILE
----

== Identifiers

An identifier is the name for an instance of a language element. In the GSQL query language, identifiers are used to name elements such as a query, a variable, or a user-defined function.
In the EBNF syntax, an identifier is referred as `name`.
It can be a sequence of letters, digits, or underscores (`"_"`).
Other punctuation characters are not supported. The initial character can only be a letter or an underscore.

.name (identifier)

[source,ebnf]
----
name := (letter | "_") [letter | digit | "_"]*
----

== Overview

Different types of data can be used in different contexts.
The EBNF syntax defines several classes of data types.
The most basic is called base type (`baseType`).
The other independent types are `FILE` and `STRING COMPRESS`.
The remaining types are either compound data types built from the independent data types, or supersets of other types.  The table below gives an overview of their definitions and their uses.

[width="100%",cols="<34%,<33%,<33%",options="header",]
|===
|EBNF term |Description |Use Case
|`+baseType+` |`+INT+`, `+UINT+`, `+FLOAT+`, `+DOUBLE+`, `+STRING+`,
`+BOOL+`, `+DATETIME,+` `+VERTEX+`, `+EDGE+`, +
+
`+JSONOBJECT+`, or `+JSONARRAY+` a|
* Base type variable
* Query return value

|`+containerType+`| `SET`, `BAG`, `LIST`, `MAP` a| * Local container variable

|`+tupleType+` |Sequence of base types a|
* User-defined tuple

|`+accumType+` |Family of specialized data objects which support
accumulation operations a|
* Accumulate and aggregate data, when traversing a set of vertices or
edges +
 +
(Details are in the xref:querying:accumulators.adoc[Query Lang Spec - Accumulators]
chapter.)

|`+FILE+` |`+FILE+` object a|
* Global sequential data object, linked to a text file

|`+parameterType+` |`+baseType+` (except `+EDGE+` or `+JSONOBJECT+`), a
set or bag of `+baseType+`, or `+FILE+` object a|
* Query parameter

a|
`+STRING COMPRESS+`

(âš **Deprecated data type. Please use `STRING` instead**)

|`STRING COMPRESS` a|
* more compact storage of STRING, *if there is a limited number of
different values and the value is rarely accessed. Otherwise, it may use
more memory.*

|`+elementType+` |`+baseType+`, `+STRING COMPRESS+`, or identifier a|
* Element for most types of container accumulators: `+SetAccum+`,
`+BagAccum+`, `+GroupByAccum+`, key of a `+MapAccum+` element

|`+type+` |`+baseType+`, `+STRING COMPRESS+`, identifier, or
`+accumType+` a|
* Element of a `+ListAccum+`, value of a `+MapAccum+` element
* Local variable

|===


== Base types

The query language supports the following _base types_, which can be declared and assigned anywhere within their scope.
Any of these base types may be used when defining a global variable, a local variable, a query return value, a parameter, part of a tuple, or an element of a container accumulator.
Accumulators are described in detail in a later section.

.EBNF
[source,ebnf]
----
baseType := INT
          | UINT
          | FLOAT
          | DOUBLE
          | STRING
          | BOOL
          | VERTEX ["<" vertexType ">"]
          | EDGE
          | JSONOBJECT
          | JSONARRAY
          | DATETIME
----



The default value of each base type is shown in the table below.
The default value is the initial value of a base type variable (see Section "Variable Types" for more details), or the default return value for some functions.

The first seven types (`INT`, `UINT`, `FLOAT`, `DOUBLE`, `BOOL`, `STRING`, and `DATETIME`) are the same ones mentioned in the "xref:ddl-and-loading:system-and-language-basics.adoc#_attribute_data_types[Attribute Data Types]" section of xref:ddl-and-loading:system-and-language-basics.adoc[_GSQL Language Reference, Part 1_].

|===
| Type | Default value | Literal

| `INT`
| `0`
| A signed integer: `-3`

| `UINT`
| `0`
| An unsigned integer: `5`

| `FLOAT`
| `0`
| A decimal: `3.14159`

| `DOUBLE`
| `0`
| A decimal with greater precision than `FLOAT`

| `BOOL`
| `false`
| `TRUE` or `FALSE`

| `STRING`
| `""`
| Characters enclosed by double quotes: `"Hello"`

| `DATETIME`
| `1970-01-01 00:00:00`
| No literal. Can be converted from a correctly formatted string with xref:querying:func/type-conversion-functions.adoc#_to_datetime[`to_datetime()`].

| `VERTEX`
| `"Unknown"`
| No literal.

| `EDGE`
| No edge: `{}`
| No literal.

| `JSONOBJECT`
| An empty object: `{}`
| No literal. Can be converted from a correctly formatted string with xref:querying:func/type-conversion-functions.adoc#_parse_json_object[`parse_json_object()`].

| `JSONARRAY`
| An empty array: `[]`
| No literal. Can be converted from a correctly formatted string with xref:querying:func/type-conversion-functions.adoc#_parse_json_array[`parse_json_array()`].
|===

[WARNING]
====
`FLOAT` and `DOUBLE` input values must be in fixed point `d.dddd` __**__format, where `d` is a digit. Output values will be printed in either fixed point for exponential notation, whichever is more compact.

The GSQL Loader can read FLOAT and DOUBLE values with exponential notation (e.g., 1.25 E-7).
====

=== Vertex
`VERTEX` is considered a base type in the GSQL query language.
Both query parameters and variables in a query body can be of type `VERTEX`.

==== Vertex types
A graph schema defines specific vertex types.
Each vertex type has its own set of attributes.
The parameter or variable type can be restricted by giving the vertex type in angle brackets `<>` after the keyword `VERTEX`.
A vertex variable declared without a specifier is called a _generic_ vertex variable.

.Generic and typed vertex variables
----
VERTEX anyVertex;
VERTEX<person> owner;
----

All vertices have a built-in attribute `type`. The built-in attribute is of type string. You can access it with the dot (`.`) operator.

For example, if you declare a vertex variable `VERTEX<person> personVertex`, then `personVertex.type` returns `"person"`.


=== Edge
`EDGE` is considered a base type in the GSQL query language.
Both query parameters and variables in a query body can be of type `EDGE`.

==== Edge types
A graph schema defines specific edge types.
Each edge type has its own set of attributes.
The parameter or variable type can be restricted by giving the edge type in angle brackets `<>` after the keyword `EDGE`.
An edge variable declared without a specifier is called a _generic_ edge variable.

.Generic and typed edge variables
----
EDGE anyEdge;
EDGE<friendship> friendEdge;
----

All edges have a built-in attribute `type`. The built-in attribute is of type string. You can access it with the dot (`.`) operator.

For example, if you define an edge variable `EDGE<friendship> friendEdge`, then `friendEdge.type` returns `"Friendship"`.


=== Vertex and Edge Attribute Types

The following table maps vertex or edge attribute types in the Data Definition Language (DDL) to GSQL query language types.
If an attribute of a vertex or edge is referenced in a GSQL query, they will be automatically converted to their corresponding data type in the GSQL query language.

|===
| DDL | GSQL Query

| `INT`
| `INT`

| `UINT`
| `UINT`

| `FLOAT`
| `FLOAT`

| `DOUBLE`
| `DOUBLE`

| `BOOL`
| `BOOL`

| `STRING`
| `STRING`

| `STRING COMPRESS`
| `STRING`

| `SET< type >`
| `SetAccum< type >`

| `LIST< type >`
| `ListAccum< type >`

| `MAP <key_type, value_type>`
| `MapAccum <key_type, value_type>`

| `DATETIME`
| `DATETIME`
|===


=== `JSONOBJECT`

A `JSONOBJECT` instance's external representation (as input and output) is a string, starting and ending with curly braces (`{}`) which enclose an unordered list of key-value pairs.
`JSONOBJECT` is immutable.
No operator is allowed to alter its value.

==== Assign a `JSONOBJECT` value to a base type variable
There is no `JSONOBJECT` literal in the GSQL query language.
Therefore, to assign value to a `JSONOBJECT` base type variable in GSQL, you need to use the `parse_json_object()` function to convert a string representation of s JSON object to a `JSONOBJECT` value.

For example, the following line declares a variable `han` with a `JSONOBJECT` value:

[,gsql]
----
JSONOBJECT han = parse_json_object("{\"f_name\": \"Han\", \"l_name\": \"Solo\"}");
----



=== `JSONARRAY`
A `JSONARRAY` is represented as a string, starting and ending with square brackets (`[]`)which enclose an ordered list of _values_.
`JSONARRAY` is immutable.
No operator is allowed to alter its value.

==== Assign a `JSONARRAY` value to a base type variable
There is no `JSONARRAY` literal in the GSQL query language.
Therefore, to assign value to a `JSONARRAY` base type variable in GSQL, you need to use the `parse_json_array()` function to convert a string representation of s JSON object to a `JSONARRAY` value.

For example, the following line declares a variable `fruits` with a `JSONARRAY` value:

[,gsql]
----
JSONARRAY fruits = parse_json_array("[\"apple\", \"banana\", \"citrus\"]");
----

== Container types
Container types include the following data types:

* `SET`
* `BAG`
* `LIST`
* `MAP`

.Container types
|===
|Type |Literal |Example |Default value

|`SET`
|Elements enclosed by parentheses, separated by commas.
|`(1, 2, 3)`
|Empty set.

|`BAG`
|Elements enclosed by parentheses, separated by commas.
|`(1, 1, 2)`
|Empty bag.

|`LIST`
|Elements enclosed by square brackets, separated by commas.
|`[1, 2, 3]`
|Empty list.

|`MAP`
|No literal.
| N/A
|Empty map.
|===

To see how container type variables are declared in a GSQL query, see xref:declaration-and-assignment-statements.adoc#_local_container_variable[Local container variables].


[#_tuple]
== Tuple

A tuple is a user-defined data structure consisting of a fixed sequence of base type variables.
Tuple types can be created and named using a `TYPEDEF` statement.
Tuples must be defined first, before any other statements in a query.

.EBNF for tuples
[source,ebnf]
----
typedef := TYPEDEF TUPLE "<" tupleFields ">" tupleType

tupleFields := (baseType fieldName) | (fieldName baseType)
           ["," (baseType fieldName) | (fieldName baseType)]*
----



A tuple can also be defined in a graph schema and then can be used as a vertex or edge attribute type. A tuple type that has been defined in the graph schema does not need to be re-defined in a query.

The vertex type `person` contains two complex attributes:

* `secretInfo` of type `SECRET_INFO`, which a user-defined tuple
* `portfolio` of type `MAP<STRING, DOUBLE>`

[#Investment_Net-schema]
.`Investment_Net` Schema
[source.wrap,gsql]
----
TYPEDEF TUPLE <age UINT (4), mothers_name STRING(20) > SECRET_INFO

CREATE VERTEX Person(PRIMARY_ID person_id STRING, portfolio MAP<STRING, DOUBLE>, secret_info SECRET_INFO)

CREATE VERTEX Stock_Order(PRIMARY_ID order_id STRING, ticker STRING, order_size UINT, price FLOAT)

CREATE UNDIRECTED EDGE make_order(FROM Person, TO Stock_Order, order_time DATETIME)

CREATE GRAPH Investment_Net (*)
----

The query below reads both the `SECRET_INFO` tuple and the portfolio MAP.
The tuple type does not need to be redefined in the query.
To read and save the map, we define a xref:querying:accumulators.adoc#_mapaccum[`MapAccum`] with the same types for key and value as the `portfolio`  attribute.
In addition, the query creates a new tuple type, `ORDER_RECORD`.

.Example Query with a user-defined tuple
[tabs]
====
Query::
+
--
[source,gsql]
----
include::appendix:example$investment_net/datatypes_udt.gsql[]
----
<1> This statement defines a new tuple `Order_Record` at the top of the query.
<2> `Secret_Info` has already been defined in <<Investment_Net-schema>>.
--

Result::
+
--
[source,gsql]
----
GSQL > RUN QUERY tupleEx("person1")
include::appendix:example$investment_net/datatypes_udt_results.json[]
----
--
====


== `STRING COMPRESS`

`STRING COMPRESS` is an integer type encoded by the system to represent string values. `STRING COMPRESS` uses less memory than `STRING`.
The `STRING COMPRESS` type is designed to act like `STRING`: data are loaded and printed just as string data, and most functions and operators which take `STRING` input can also take `STRING COMPRESS` input. The difference is in how the data are stored internally.
A `STRING COMPRESS` value can be obtained from a `STRING_SET COMPRESS` or `STRING_LIST COMPRESS` attribute or from converting a `STRING` value.

[WARNING]
====
Using `STRING COMPRESS` instead of `STRING` is a trade-off: smaller storage vs. slower access times.
The storage space will only be smaller if (1) the original strings are long, and (2) there are only a small number of different strings.
Performance will always be slower; the slowdown is greater if the `STRING COMPRESS` attributes are accessed more often.

We recommend performing comparison tests for both performance and memory usage before settling on `STRING COMPRESS`.
====

`STRING COMPRESS` type is beneficial for sets of string values when the same values are used multiple times.
In practice, `STRING COMPRESS` are most useful for container accumulators like `ListAccum<STRING COMPRESS>` or `SetAccum<STRING COMPRESS>`.

An accumulator containing `STRING COMPRESS` stores the dictionary when it is assigned an attribute value or from another accumulator containing `STRING COMPRESS`.
An accumulator containing `STRING COMPRESS` can store multiple dictionaries.
A `STRING` value can be converted to a `STRING COMPRESS` value only if the value is in the dictionaries.
If the `STRING` value is not in the dictionaries, the original string value is saved.
A `STRING COMPRESS` value can be automatically converted to a `STRING` value.

When a `STRING COMPRESS` value is output (e.g. by a xref:querying:output-statements-and-file-objects.adoc#_print_statement_api_v2[`PRINT` statement]), it is shown as a `STRING`.

Below is an example query that uses the `STRING COMPRESS` type.

[WARNING]
====
`STRING COMPRESS` is not a base type.
====

.STRING COMPRESS example query
[tabs]
====
Query::
+
--
[source,gsql]
----
include::appendix:example$investment_net/datatypes_stringcompress.gsql[]
----
--

Results::
+
--
[source,gsql]
----
GSQL > RUN QUERY stringCompressEx("person12")
include::appendix:example$investment_net/datatypes_stringcompress_results.json[]
----
--
====


== `FILE` Object

A `FILE` object is a sequential data storage object, associated with a text file on the local machine.

[NOTE]
====
When referring to a `FILE` object, we always capitalize the word `FILE` to distinguish it from ordinary files.
====

When a `FILE` object is declared, associated with a particular text file, any existing content in the text file will be erased.
During the execution of the query, content written to the `FILE` will be appended to the `FILE`.
When the query where the `FILE` was declared finishes running, the `FILE` contents are saved to the text file.

A `FILE` object can be passed as a parameter to another query. When a query receives a `FILE` object as a parameter, it can append data to that `FILE`, as can every other query which receives this `FILE` object as a parameter.

== Query parameter types

Input parameters to a query can be base type (except `EDGE` , `JSONARRAY`, or `JSONOBJECT`).
A parameter can also be a `SET` or `BAG` which uses base type (except `EDGE` , `JSONARRAY`, or `JSONOBJECT`) as the element type. A `FILE` object can also be a parameter.
Within the query, `SET` and `BAG` are converted to xref:accumulators.adoc#_setaccum[`SetAccum`] and xref:accumulators.adoc#_bagaccum[`BagAccum`], respectively.

[WARNING]
====
A query parameter is immutable. It cannot be assigned a new value within the query.

The `FILE` object is a special case.
It is passed by reference, meaning that the receiving query gets a link to the original `FILE` object.
The receiving query can write to the `FILE` object.
====

.EBNF
[source,ebnf]
----
parameterType := INT
               | UINT
               | FLOAT
               | DOUBLE
               | STRING
               | BOOL
               | VERTEX ["<" vertexType ">"]
               | DATETIME
               | [ SET | BAG ] "<" baseType ">"
               | FILE
----



.Examples of collection type parameters
[source,gsql]
----
(SET<VERTEX<person> p1, BAG<INT> ids, FILE f1)
----


