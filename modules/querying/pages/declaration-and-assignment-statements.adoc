= Declaration and Assignment Statements
:description: Declaration and assignment statements in the GSQL querying language.

In GSQL, different types of variables and objects follow different rules when it comes to variable declaration and assignment.
This section discusses the different types of declaration and assignment statements and covers the following subset of the EBNF syntax:

.EBNF for declaration and assignment
[source,ebnf]
----
## Declarations ##
accumDeclStmt :=
          accumType localAccumName ["=" constant]
                    ["," localASccumName ["=" constant]]*
        | accumType globaAccumName ["=" constant]
                    ["," GlobalAccumName ["=" constant]]*
localAccumName := "@"accumName;
globalAccumName := "@@"accumName;

baseDeclStmt    := baseType name ["=" constant] ["," name ["=" constant]]*
fileDeclStmt := FILE fileVar "(" filePath ")"
fileVar := name

localVarDeclStmt := baseType varName "=" expr

vSetVarDeclStmt := vertexSetName ["(" vertexType ")"] "=" (seedSet | simpleSet | selectBlock)

simpleSet := vertexSetName
           | "(" simpleSet ")"
           | simpleSet (UNION | INTERSECT | MINUS) simpleSet

seedSet := "{" [seed ["," seed ]*] "}"
seed := '_'
      | ANY
      | vertexSetName
      | globalAccumName
      | vertexType ".*"
      | paramName
      | "SelectVertex" selectVertParams

selectVertParams := "(" filePath "," columnId "," (columnId | name) ","
          stringLiteral "," (TRUE | FALSE) ")" ["." FILTER "(" condition ")"]

columnId := "$" (integer | stringLiteral)

## Assignment Statements ##
assignStmt := name "=" expr
            | name "." attrName "=" expr

attrAccumStmt := name "." attrName "+=" expr

lAccumAssignStmt := vertexAlias "." localAccumName ("+="| "=") expr

gAccumAssignStmt :=  globalAccumName ("+=" | "=") expr

loadAccumStmt := globalAccumName "=" "{" LOADACCUM loadAccumParams
                                  ["," LOADACCUM loadAccumParams]* "}"

loadAccumParams := "(" filePath "," columnId ["," columnId]* ","
          stringLiteral "," (TRUE | FALSE) ")" ["." FILTER "(" condition ")"]

## Function Call Statement ##
funcCallStmt := name ["<" type ["," type]* ">"] "(" [argList] ")"
              | globaAccumName ("." funcName "(" [argList] ")")+
              | "reset_collection_accum" "(" accumName ")"

argList := expr ["," expr]*
----

== Variable scopes

Different types of variable declarations use different scoping rules. There are two types of scoping rules in a GSQL query:

* xref:querying:declaration-and-assignment-statements.adoc#_block_scoping[Block scoping]
* xref:querying:declaration-and-assignment-statements.adoc#_global_scoping[Global scoping]

[#_block_scoping]
=== Block scoping

In GSQL, curly brackets, as well as `IF .. THEN`, `ELSE`, `+WHILE ... DO+`, `+FOREACH ... DO+` statements create a block.A `SELECT` statement also creates a block.A block-scoped variable declared inside a block scope is only accessible inside that scope.

Additionally, variables declared in a lower scope can use the same name as a variable already declared in a higher scope.The lower-scope declaration will take precedence over the higher-scope declaration until the end of the lower scope.

The following types of variables use block scoping:

* xref:querying:accumulators.adoc[Accumulators]
* xref:querying:declaration-and-assignment-statements.adoc#_base_type_variables[Base type variables]
* xref:querying:declaration-and-assignment-statements.adoc#_local_base_type_variables[Local base type variables]
* xref:querying:declaration-and-assignment-statements.adoc#_file_objects[File objects]
* xref:querying:select-statement/README.adoc#_vertex_and_edge_aliases[Vertex or edge aliases]

[#_global_scoping]
=== Global scoping

A global-scoped variable is always accessible anywhere in the query once it has been declared regardless of where it is declared.One also cannot declare another variable with the same name as a global-scoped variable that has already been declared.

The following types of variables use global scoping:

* xref:querying:declaration-and-assignment-statements.adoc#_vertex_set_variables[Vertex set variables]

== Declaration Statements

There are six types of variable declarations in a GSQL query:

* Accumulator
* Base type variable
* Local base type variable
* Vertex set
* File object
* Vertex or edge aliases

The first five types each have their own declaration statement syntax and are covered in this section. Aliases are declared implicitly in a `SELECT` statement.

=== Accumulators

Accumulator declaration is discussed in xref:querying:accumulators.adoc#_declaration_of_accumulators[Accumulators].

[#_base_type_variables]
=== Base type variables

In a GSQL query body, variables holding values of types `INT`, `UINT`, `FLOAT`, `DOUBLE`, `BOOL`, `STRING`, `DATETIME`, `VERTEX`, `EDGE`, `JSONOBJECT` and `JSONARRAY` are called base type variables.The scope of a base type variable is from the point of declaration until the end of the block where its declaration took place.

.EBNF for base type variable declaration
[source,ebnf]
----
baseVarDeclStmt    := baseType name ["=" expr]["," name ["=" expr]]*
----

A base type variable can be declared and accessed anywhere in the query.
To declare a base type variable, specify the data type and the variable name.
Optionally, you can initialize the variable by assigning it a value with the assignment operator (`=`) and the desired value on the right side.
You can declare multiple variables of the same type in a single declaration statement.

[source,gsql]
----
include::appendix:example$minimal_net/declaration_base_type_variables.gsql[]
----

When a base type variable is assigned a new value in an `ACCUM` or `POST-ACCUM` clause, the change will not take place until exiting the clause.
Therefore, if there are multiple assignment statements for the same base type variable in an `ACCUM` or `POST-ACCUM` clause, only the last one will take effect.

For example, in the following query, a base type variable is assigned a new value in the `ACCUM` clause, but the change will not take place until the clause ends.
Therefore, the accumulator will not receive the value and will hold a value of 0 at the end of the query.

[source,gsql]
----
include::appendix:example$social_net/declaration_base_type_variables_value_assignment.gsql[]
----

[#_local_base_type_variables]
=== Local base type variables

Base type variables declared in a DML-sub statement, such as in a statement inside a `ACCUM`, `POST-ACCUM`, or `UPDATE SET` clause, are called _local_ _base type variables_.

Local base type variables are block-scoped and are accessible in the block where they are declared only.
Within a local base type variable's scope, you cannot declare another local base type variable, local container variable, or local tuple variable with the same name at the same level.
However, you can declare a local base type variable or local container variable with the same name at a lower level, where the lower-level declaration will take precedence over the previous declaration.

In a `POST-ACCUM` clause, each local base type variable may only be used in source vertex statements or only in target vertex statements, not both.

.EBNF for local base type variable declaration and initialization
[source,ebnf]
----
localVarDeclStmt := baseType varName "=" expr
----

Local base type variables are not subject to the assignment restrictions of regular base type variables. Their values can be updated inside an `ACCUM` or `POST-ACCUM` clause and the change will take place immediately.

==== Example:

[tabs]
====
Query::
+
--
.Base type variable declaration in DML statements
[source,gsql]
----
include::appendix:example$social_net/declaration_base_type_variables_dml_statements.gsql[]
----
--
Result::
+
--
.localVariable Query Results
[source,gsql]
----
GSQL > RUN QUERY local_variable("person1")
include::appendix:example$social_net/declaration_base_type_variables_dml_statements_results.json[]
----
--
====

[#_local_container_variable]
=== Local container variable
Variables declared inside a DML-block storing container type values are called _local container type variables_.
Their values can be updated inside an `ACCUM` or `POST-ACCUM` clause and the change will take place immediately.

Local container variables can store values of a specified type.
The following types are allowed:

|===
|Container type |Element type

|`SET`
|`INT`, `UINT`, `DOUBLE`, `FLOAT`, `STRING`, `STRING COMPRESS`, `JSON OBJECT`, `JSONARRAY`,`BOOL` `EDGE`, `DATETIME`, tuple

|`BAG`
|`INT`, `UINT`, `DOUBLE`, `FLOAT`, `STRING`, `STRING COMPRESS`, `JSON OBJECT`, `JSONARRAY`,`BOOL`, `VERTEX` `EDGE`, `DATETIME`, tuple

|`LIST`
|`INT`, `UINT`, `DOUBLE`, `FLOAT`, `STRING`, `STRING COMPRESS`, `JSON OBJECT`, `JSONARRAY`,`BOOL`, `VERTEX` `EDGE`, `DATETIME`, `ListAccum` (Up to 3 levels of nesting), tuple

|`MAP`
a|* Key types: `INT`, `UINT`, `DOUBLE`, `FLOAT`, `STRING`, `STRING COMPRESS`, `JSON OBJECT`, `JSONARRAY`,`BOOL`, `VERTEX` `EDGE`, `DATETIME`, tuple
* Value types: `INT`, `UINT`, `DOUBLE`, `FLOAT`, `STRING`, `STRING COMPRESS`, `JSON OBJECT`, `JSONARRAY`,`BOOL`, `VERTEX` `EDGE`, `DATETIME`, tuple, any accumulator type except for `HeapAccum`
|===


You must declare which type the container variable will be storing when you declare the container variable.

.Local container variable declaration
[tabs]
====
EBNF::
+
--
[source,ebnf]
----
localContainerDeclStmt := containerType "<" type ">" varName "=" expr
----
--
Example::
+
--
[source,gsql]
----
SET<INT> set1 = (1, 2, 3) <1>
----
<1> The declaration can only take place in a DML block.
--
====

Local container variables are block-scoped and are accessible in the block where they are declared only.
Within a local container variable’s scope, you cannot declare another local container variable, local tuple variable, or local base type variable with the same name at the same level.
However, you can declare a variable with the same name at a lower level, where the lower-level declaration will override the previous declaration.

In a `POST-ACCUM` clause, each local container variable may only be used in source vertex statements or only in target vertex statements, not both.


==== Query example
In the following example, the `SELECT` statement in the main query declares three local container variables, each containing:

* A base type
* A user-defined tuple
* An anonymous tuple

[tabs]
====
Main query::
+
--
[source,gsql]
----
include::appendix:example$poc_graph/declaration_local_container_main.gsql[]
----
--
Subquery 1::
+
--
----
include::appendix:example$poc_graph/declaration_local_container_sub_1.gsql[]
----
--
Subquery 2::
+
--
----
include::appendix:example$poc_graph/declaration_local_container_sub_2.gsql[]
----
--
====

[#_local_tuple_variable]
=== Local tuple variable
Variables declared inside a DML-block storing tuple values are called _local tuple variables_.
The value of a local tuple variable is assigned at declaration.
The value of a local tuple variable can be updated inside an `ACCUM` or `POST-ACCUM` clause and the change will take place immediately.

Local tuple variables are block-scoped and are accessible in the block where they are declared only.
Within a local tuple variable’s scope, you cannot declare another local tuple variable, local container variable, or local base type variable with the same name at the same level.
However, you can declare a variable with the same name at a lower level, where the lower-level declaration will override the previous declaration.

You can declare tuple variables of defined types and anonymous types.

==== Syntax
[,ebnf]
----
assignDeclLocalTuple := (tupleTypeName | anonymousTupleType) localTupleVal = expr
----

==== Example

[,gsql]
----
include::appendix:example$poc_graph/declaration_local_tuple_variable.gsql[]
----
<1> This statement defines a local tuple variable `a` with a defined tuple type.
<2> This statement defines a local tuple variable `b` with an anonymous tuple type.
Beside using another tuple type, you can also return an anonymous tuple from a xref:operators-and-expressions.adoc#_subqueries[subquery] to assign value to the local tuple variable.

[#_vertex_set_variables]
=== Vertex set variables

Variables that contain a set of one or more vertices are called vertex set variables.
Vertex set variables play a special role within GSQL queries.
They are used for both the input and output of `SELECT` statements.

NOTE: In Syntax V1, before the first `SELECT` statement in a query, a vertex set variable must be declared and initialized.
This initial vertex set is called the _seed set_.
The current default syntax - Syntax V2 - no longer has this requirement.

Vertex set variables are global-scoped.
They are also the only type of variable that isn't explicitly typed during declaration.
To declare a vertex set variable, assign an initial set of vertices to the variable name.

.EBNF for Vertex Set Variable Declaration
[source.wrap,ebnf]
----
vSetVarDeclStmt := vertexSetName ["(" vertexType ")"] "=" (seedSet | simpleSet | selectBlock)

simpleSet := vertexSetName
      | "(" simpleSet ")"
      | simpleSet (UNION | INTERSECT | MINUS) simpleSet

seedSet := "{" [seed ["," seed ]*] "}"
seed := '_'
      | ANY
      | vertexSetName
      | globalAccumName
      | vertexType ".*"
      | paramName
      | "SelectVertex" selectVertParams

selectVertParams := "(" filePath "," columnId "," (columnId | name) ","
     stringLiteral "," (TRUE | FALSE) ")" ["." FILTER "(" condition ")"]

columnId := "$" (integer | stringLiteral)
----

The query below lists all ways of assigning a vertex set variable an initial set of vertices (that is, forming a seed set).

* A vertex parameter, untyped or typed, enclosed in curly brackets
* A vertex set parameter, untyped or typed
* A global `SetAccum<VERTEX>` accumulator, untyped or typed
* A vertex type followed by `.*` to indicate all vertices of that type, optionally enclosed in curly brackets.
* `_` or `ANY` to indicate all vertices, optionally enclosed in curly brackets.
* A list of vertex IDs in an external file
* Copy of another vertex set
* A combination of individual vertices, vertex set parameters, or base type variables, enclosed in curly brackets
* Union of vertex set variables

.Vertex set example
[source.wrap,gsql]
----
include::appendix:example$social_net/declaration_vertex_set_example.gsql[]
----
<1> See xref:func/vertex-methods.adoc#_selectvertex[SelectVertex()].


When declaring a vertex set variable, you may opt to specify the vertex set type for the vertex set variable by .
If the vertex set variable set type is not specified explicitly, the system determines the type implicitly by the vertex set value.
The type can be `ANY`, `_` (equivalent to `ANY`), or any explicit vertex type(s). See the EBNF grammar rule `vertexEdgeType`.


==== Assignment

After a vertex set variable is declared, the vertex type of the vertex set variable is immutable.
Every assignment (e.g. `SELECT` statement) to this vertex set variable must match the type.
The following is an example in which we must declare the vertex set variable type.

.Vertex set variable type
[source,gsql]
----
include::appendix:example$social_net/declaration_vertex_set_variable_type.gsql[]
----

In the above example, the query returns the set of vertices after a 5-step traversal from the input `person` vertex.
If we declare the vertex set variable `S` without explicitly giving a type, because the type of vertex parameter `m1` is `person`, the GSQL engine will implicitly assign S to be `person` type.
However, if `S` is assigned to `person` type, the `SELECT` statement inside the `WHILE` loop causes a type-checking error, because the `SELECT` block will generate all connected vertices, including non-person vertices.
Therefore, `S` must be declared as an ANY-type vertex set variable.

[#_file_objects]
=== `FILE` objects

A `FILE` object is a sequential text storage object, associated with a text file on the local machine.

.EBNF for FILE object declaration
[source,ebnf]
----
fileDeclStmt := FILE fileVar "(" filePath ")"
fileVar := name
----


When a `FILE` object is declared, associated with a particular text file, any existing content in the text file will be erased.
During the execution of the query, content written to or printed to the `FILE` object will be appended to the `FILE` object.
When the query where the `FILE` object is declared finishes running, the content of the `FILE` object is saved to the text file.

==== Example

.File object query example
[source,gsql]
----
include::appendix:example$work_net/declaration_file_object_query.gsql[]
----



== Assignment and Accumulate Statements

Assignment statements are used to set or update the value of a variable after it has been declared.
This applies to base type variables, vertex set variables, and accumulators.
Accumulators also have the special += accumulate statement, which was discussed in the Accumulator section.
Assignment statements can use expressions to define the new value of the variable.

.EBNF for Assignment Statements
[source,ebnf]
----
## Assignment Statement ##
assignStmt := name "=" expr         # baseType variable, vertex set variable
            | name "." name "=" expr      # attribute of a vertex or edge

attrAccumStmt := name "." attrName "+=" expr

lAccumAssignStmt := vertexAlias "." localAccumName ("+="| "=") expr

gAccumAssignStmt :=  globalAccumName ("+=" | "=") expr

loadAccumStmt := globalAccumName "=" "{" "LOADACCUM" loadAccumParam
                                  ["," "LOADACCUM" loadAccumParams]* "}"
----



[NOTE]
====
Vertex and edge (non-accumulator) attributes can use the += operator in an ACCUM or POST-ACCUM clause to perform parallel accumulation.

[source,ebnf]
----
attrAccumStmt := name "."attrName "+=" expr
----

====

=== Restrictions on Assignment Statements

In general, assignment statements can take place anywhere after the variable has been declared.  However, there are some restrictions. These restrictions apply to "inner level" statements which are within the body of a higher-level statement:

* The `ACCUM` or `POST-ACCUM` clause of a `SELECT` statement
* The `SET` clause of an `UPDATE` statement
* The body of a `FOREACH` statement

[WARNING]
====
* Global accumulator assignment is not permitted within the body of `SELECT` or `UPDATE` statements
* Base type variable assignment is permitted in `ACCUM` or `POST-ACCUM` clauses, but the change in value will not take place until exiting the clause. Therefore, if there are multiple assignment statements for the same variable, only the final one will take effect.
* Vertex attribute assignment is not permitted in an `ACCUM` clause. However, edge attribute assignment is permitted. This is because the `ACCUM` clause iterates over an edge set.
* There are additional restrictions within `FOREACH` loops for the loop variable. See the Data Modification section.
====

=== `LOADACCUM` Statement

[source,ebnf]
----
loadAccumStmt := globalAccumName "=" "{" LOADACCUM loadAccumParams
                                  ["," LOADACCUM loadAccumParams]* "}"

loadAccumParams := "(" filePath "," columnId ["," [columnId]* ","
          stringLiteral "," (TRUE | FALSE) ")" ["."FILTER "(" condition ")"]
columnId := "$"(integer | stringLiteral)
----

`LOADACCUM()` can initialize a global accumulator by loading data from a file. `LOADACCUM()` has 3+n parameters explained in the table below, where n is the number of fields in the accumulator.

[WARNING]
====
Any accumulator using generic `VERTEX` as an element type cannot be initialized by `LOADACCUM()`.
====

==== Parameters

|===
| Name | Type | Description

| `filePath`
| String
| The absolute file path of the input file to be read. A relative path is not supported.

| `columnId`
| String or number
| The column position(s) or column name(s) of the data file that supply data values to each field of the accumulator.

| `separator`
| Single-character string
| The separator of columns.

| `header`
| Boolean
| Whether this file has a header.
|===

One assignment statement can have multiple `LOADACCUM()` function calls. However, every `LOADACCUM()` referring to the same file in the same assignment statement must use the same separator and header parameter values.

==== Example

.LoadAccum example
[tabs]
====
Data::
+
--
.loadAccumInput.csv
[source,csv]
----
person1,1,"test1",3
person5,2,"test2",4
person6,3,"test3",5
----
--
Query::
+
--
[source,gsql]
----
include::appendix:example$social_net/declaration_multiple_loadaccum.gsql[]
----
--
Result::
+
--
[source,gsql]
----
GSQL > RUN QUERY load_accum_ex("/file_directory/loadAccumInput.csv")
include::appendix:example$social_net/declaration_multiple_loadaccum_results.json[]
----
--
====


== Function Call Statements
[source,ebnf]
----
funcCallStmt := name ["<" type ["," type]* ">"] "(" [argList] ")"
              | globalAccumName ("." funcName "(" [argList] ")")+
              | "reset_collection_accum" "(" accumName ")"

argList := expr ["," expr]*
----

Typically, a function call returns a value and so is part of an xref:querying:operators-and-expressions.adoc[expression].
In some cases, however, the function does not return a value (i.e., returns `VOID`) or the return value can be ignored, so the function call can be used as an entire statement. This is a Function Call Statement.

.Examples of Function Call statements
[source,gsql]
----
ListAccum<STRING> @@list_acc;
BagAccum<INT> @@bag_acc;
...
// Examples of function call statements
@@list_acc.clear();
@@bag_acc.removeAll(0);
----

=== Clear Collection Accumulators

Collection accumulators (e.g., `ListAccum`, `SetAccum`, `MapAccum`) grow in size as data is added. Particularly for vertex-attached accumulators, if the number of vertices is large, their memory consumption can be significant.
It can improve system performance to clear or reset collection accumulators during a query as soon as their data is no longer needed.
Running the `reset_collection_accum()` function resets the collection(s) to be zero-length (empty).
If the argument is a vertex-attached accumulator, then the entire set of accumulators is reset.

[source,ebnf]
----
"reset_collection_accum" "(" accumName ")"
----

[CAUTION]
====
`reset_collection_accum` only works in DISTRIBUTED mode queries.  If the query is not in distributed mode, the reset does not take place.
====

[tabs]
====
Query::
+
--
[source,gsql]
----
include::appendix:example$work_net/declaration_clear_collection_accum.gsql[]
----
--
Results::
+
--
[source,json]
----
include::appendix:example$work_net/declaration_clear_collection_accum_results.json[]
----
--
====