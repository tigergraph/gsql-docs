= Operators and Expressions

An _expression_ is a combination of fixed values, variables, operators, function calls, and groupings that specify a computation, resulting in a data value. This section of the specification describes the literals (fixed values), operators, and functions available in the GSQL query language. It covers the subset of the EBNF definitions shown below.

However, more so than in other sections of the specification, syntax alone is not an adequate description. The semantics (functionality) of the particular operators and functions are an essential complement to the syntax.

.EBNF for Operations, Functions, and Expressions
[source,ebnf]
----
constant := numeric | stringLiteral | TRUE | FALSE | GSQL_UINT_MAX
          | GSQL_INT_MAX | GSQL_INT_MIN | TO_DATETIME "(" stringLiteral ")"

mathOperator := "*" | "/" | "%" | "+" | "-" | "<<" | ">>" | "&" | "|"

condition := expr
           | expr comparisonOperator expr
           | expr [ NOT ] IN setBagExpr
           | expr IS [ NOT ] NULL
            | expr BETWEEN expr AND expr
           | "(" condition ")"
           | NOT condition
           | condition (AND | OR) condition
           | (TRUE | FALSE)
           | expr [NOT] LIKE expr [ESCAPE escape_char]

comparisonOperator := "<" | "<=" | ">" | ">=" | "==" | "!="

aggregator := COUNT | MAX | MIN | AVG | SUM

expr := name
    | globalAccumName
		| name ".type"
		| name "." name
		| name "." localAccumName ["\'"]
		| name "." name "." name "(" [argList] ")"
        | name "." name "(" [argList] ")" [ "." FILTER "(" condition ")" ]
		| name ["<" type ["," type]* ">"] "(" argList] ")"
		| name "." localAccumName ("." name "(" [argList] ")")+ ["." name]
		| globalAccumName ("."name "(" [argList] ")")+ ["."name]
		| COALESCE "(" [argList] ")"
		| aggregator "(" [DISTINCT] setBagExpr ")"
		| ISEMPTY "(" setBagExpr ")"
		| expr mathOperator expr
		| "-" expr
		| "(" expr ")"
		| "(" argList "->" argList ")"	// key value pair for MapAccum
		| "[" argList "]"               // a list
		| constant
		| setBagExpr
		| name "(" argList ")"          // function call or a tuple object
		
setBagExpr := name
        | globalAccumName
    	  | name "." name
		    | name "." localAccumName
		    | name "." localAccumName ("." name "(" [argList] ")")+
		    | name "." name "(" [argList] ")" [ "." FILTER "(" condition ")" ]
		    | globalAccumName ("." name "(" [argList] ")")+
		    | setBagExpr (UNION | INTERSECT | MINUS) setBagExpr
		    | "(" argList ")"
		    | "(" setBagExpr ")"


argList := expr ["," expr]*
----



== Constants

[source,ebnf]
----
constant := numeric | stringLiteral | TRUE | FALSE | GSQL_UINT_MAX
          | GSQL_INT_MAX | GSQL_INT_MIN | TO_DATETIME "(" stringLiteral ")"
----

Each primitive data type supports constant values:

|===
| Data Type | Constant | Examples

| Numeric types (`INT`, `UINT`, `FLOAT`, `DOUBLE`)
| `numeric`
| 123  -5  45.67  2.0e-0.5

| `UINT`
| `GSQL_UINT_MAX`
|

| `INT`
| `GSQL_INT_MAX`  `GSQL_INT_MIN`
|

| `boolean`
| `TRUE`  `FALSE`
|

| `string`
| `stringLiteral`
| `"atoz@com"`  `"0.25"`
|===

`GSL_UINT_MAX` = 2 {caret} 64 - 1 = 18446744073709551615

`GSQL_INT_MAX` = 2 {caret} 63 - 1 =  9223372036854775807

`GSQL_INT_MIN` = -2 {caret} 63     = -9223372036854775808

== Operators

An operator is a keyword token that performs a specific computational function to return a resulting value, using the adjacent expressions (its operands) as input values.  Both operators and functions compute a result from one or more inputs, but syntactically they are different. The most familiar operators are the mathematical operators for addition  `+`  and subtraction  `-` .

[NOTE]
====
Tip: The operators listed in this section are designed to behave like the operators in MySQL.
====

=== Mathematical Operators and Expressions

We support the following standard mathematical operators and meanings. The latter four (`<<` `>>`  `&` `|`) are for bitwise operations.  See the section below: "Bit Operators".

[source,ebnf]
----
mathOperator := "*" | "/" | "%" | "+" | "-" | "<<" | ">>" | "&" | "|"
----

Operator precedences are shown in the following list, from the highest precedence to the lowest. Operators that are shown together on a line have the same precedence:

.Operator Precedence, highest to lowest
[source,test]
----
*, /, %
-, +
<<, >>
&
|
==, >=, >, <=, <, !=
----


==== Example
[tabs]
====
Query::
+
--
.Example 1. Math Operators + - * /
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_math_operators.gsql[]
----
--
Results::
+
--
.mathOperators.json Results
[source,gsql]
----
GSQL > RUN QUERY math_operators()
include::appendix:example$minimal_net/operators_expressions_math_operators_results.json[]
----
--
====


=== Boolean Operators

We support the standard Boolean operators and standard order of precedence: `AND`, `OR`, `NOT`

=== Bit Operators

Bit operators (`<<` `>>` `&` `|`) operate on integers and return an integer.

.Bit Operators
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_bit_operators.gsql[]
----

=== String Operators

The `+` operator can be used for concatenating strings.

[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_string_operators.gsql[]
----

=== Tuple Fields

The fields of the tuple can be accessed using the dot operator.

== Comparison Operators and Conditions

A condition is an expression that evaluates to a boolean value of either true or false. One type of condition uses the familiar comparison operators. A comparison operator compares two numeric or string values.

[source,ebnf]
----
comparisonOperator := "<" | "<=" | ">" | ">=" | "==" | "!="

condition := expr
           | expr comparisonOperator expr
           | expr [ NOT ] IN setBagExpr
           | expr IS [ NOT ] NULL
           | expr BETWEEN expr AND expr
           | "(" condition ")"
           | NOT condition
           | condition (AND | OR) condition
           | (TRUE | FALSE)
           | expr [NOT] LIKE expr [ESCAPE escape_char]
----

Strings are compared based on standard lexicographical ordering: +
(space) < (digit) < (uppercase_letter) < (lowercase_letter).


=== BETWEEN ... AND ...

The expression `expr1 BETWEEN expr2 AND expr3` is `true` if the value expr1 is in the range from expr2 to expr3, including the endpoint values. Each expression must be numeric.

`expr1 BETWEEN expr2 AND expr3` is equivalent to `expr1 <= expr3 AND expr1 >= expr2`.

.`BETWEEN AND` example
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_between_and.gsql[]
----

=== IS NULL, IS NOT NULL

`IS NULL` and `IS NOT NULL` can be used for checking whether an optional parameter is given any value.


==== Example

[tabs]
====
Query::
+
--
.IS NULL example
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_is_null.gsql[]
----
--
Results::
+
--
.parameterIsNULL.json Results
[source,gsql]
----
GSQL > RUN QUERY parameter_is_null(_)
include::appendix:example$minimal_net/operators_expressions_is_null_example_null.json[]

GSQL > RUN QUERY parameter_is_null(3)
include::appendix:example$minimal_net/operators_expressions_is_null_example_3.json[]
----
--
====


[WARNING]
====
Every attribute value stored in GSQL is a valid value, so IS NULL and IS NOT NULL is only effective for query parameters.
====

=== LIKE

[source,gsql]
----
expr [NOT] LIKE expr [ESCAPE escape_char]
----

The `LIKE` operator is used for string pattern matching and can only be used in `WHERE` clauses. The expression `string1 LIKE string_pattern` evaluates to boolean true if `string1` matches the pattern in `string_pattern`; otherwise, it is false.

Both operands must be strings. Additionally, while `string1` can be a function call (e.g. `lower(string_variable)`, `string_pattern` must be a string literal or a parameter. `string_pattern` cannot be the result of concatenating other strings, nor can it be a function call.

A `string_pattern` can contain characters as well as the following wildcard and other special symbols, in order to express a pattern (`<char_list>` indicates a placeholder):

|===
| Character or syntax | Description | Example

| `%`
| Matches zero or more characters.
| `%abc%` matches any string which contains the sequence `"abc"`.

| `_`(underscore)
| Matches any single character.
| ``_abc_e``matches any 6-character string where the 2nd to 4th characters are `"abc"` and the last character is `"e"`.

| `[<char_list>]`
| Matches any character in a char list. A char list is a concatenated character set, with no separators.
| ``[Tiger]``matches either `T`, `i`, `g`, `e`, or `r`.

| `+[^<char_list>]+`
| Matches any character NOT in a char list.
| ``+[^qxz]+``matches any character other than `q`, `x`, or `z`.

| `[!<char_list>]`
| Matches any character NOT in a char list.
|

| `α-β`
| (Special syntax within a char list) matches a character in the range from α to β. A char list can have multiple ranges.
| ``[a-mA-M0-3]``matches a letter from a to m, upper or lower case, or a digit from 0 to 3.

| `\\`
| (Special syntax within a char list)  matches the character `\`
|

| `\\]`
| (Special syntax within a char list) matches the character `]` No special treatment is needed for [ inside a char list.
| ``%[\\]!]``matches any string which ends with either `]` or `!`
|===

.Matching backslashes
[NOTE]
====
When you load a string that contains a backslash into TigerGraph, the backslash itself is escaped with another backslash.
In a string pattern, two backslashes match one backslash in an input string.
This applies to both backslashes - the one used to escape the other and the one being escaped.

For example, if the string `\abc` is loaded into `Vertex.attribute`.
Then:

* The actual value of `Vertex.attribute` in the graph is `"\\abc"`.
The size of the string is 4, because the first backslash is used to escape the second backslash and doesn't count towards the size of the string.
* `Vertex.attribute == "\\abc"` returns true, because a backslash is required to escape the other backslash. 
* `Vertex.attribute LIKE "\\\\abc"` returns true, because it takes two backslashes in a string pattern to match one backslash in a string.

====

==== `ESCAPE escape_char`

The optional `ESCAPE escape_char` clause is used to define an escape character. When `escape_char` occurs in `string_pattern`, then the next character is interpreted literally, instead of as a pattern matching operator. For example, if we want to specify the pattern "any string ending with the `'%'` character", we could use +
`"%\%" ESCAPE "\"`

The first `"%"` has its usual pattern-matching meaning "zero or more characters".  +
`"\%"` means a literal percentage character, because it starts with the escape character `"\"`.

==== Example

[tabs]
====
Query Without Parameters::
+
--
.Example query using LIKE operator
[source,gsql]
----
include::appendix:example$social_net/operators_expressions_query_without_parameters.gsql[]
----
--
Query With Parameters::
+
--
.Example parameterized query
[source,gsql]
----
include::appendix:example$social_net/operators_expressions_query_with_parameters.gsql[]

----
--
Results::
+
--
[source,json]
----
include::appendix:example$social_net/operators_expressions_query_without_parameters_results.json[]
----
--
====

== Vertex, edge, and accumulator attributes

=== Accessing attributes

Attributes on vertices or edges are defined in the graph schema. Additionally, each vertex and edge has a built-in STRING attribute called *type* which represents the user-defined type of that edge or vertex. These attributes, including *type,* can be accessed for a particular edge or vertex with the dot operator:

.Accessing attributes with a known name.
[source,ebnf]
----
name ".type"   // read only. Returns vertexType or edgeType of name
name "." attrName // read/write. Accesses attribute called attrName
----


.Dynamic query support
[NOTE]
====
The name of the attribute can be parameterized using the *getAttr* and *setAttr* xref:querying:func/vertex-methods.adoc[vertex functions], described later in this section. This allows you to write xref:querying:query-operations.adoc#_dynamic_querying[dynamic query] procedures where the attribute names are specified when you run the query.
====

For example, the following code snippet shows two different SELECT statements which produce equivalent results. The first uses the dot operator on the vertex variable *v* to access the "subject" attribute, which is defined in the graph schema. The FROM clause in the first SELECT statement necessitates that any target vertices will be of type "post" (also defined in the graph schema). The second SELECT schema checks that the vertex variable v's type is a "post" vertex by using the dot operator to access the built-in *type* attribute.

==== Example
[tabs]
====
Query::
+
--
.Accessing vertex variable attributes
[source,gsql]
----
include::appendix:example$social_net/operators_expressions_access_vertex_variable_attributes.gsql[]
----
--

Results::
+
--
.Results for Query `coffeeRelatedPosts`
[source,json]
----
include::appendix:example$social_net/operators_expressions_access_vertex_variable_attributes_results.json[]
----
--
====


=== Accumulator Functions

This section describes functions that apply to all or most accumulators. Other accumulator functions for each accumulator type are illustrated in the "Accumulator Type" section.

==== Previous value of accumulator

The tick operator `'` can be used to read the value of an accumulator as it was at the start an ACCUM clause, before any changes that took place within the ACCUM clause. It can only be used in the POST-ACCUM clause. A typical use is to compare the value of the accumulator before and after the ACCUM clause.  The PageRank algorithm provides a good example:

[source,gsql]
----
v = SELECT s
    FROM start:s - (e_type:e) -> :t
    ACCUM t.@received_score += s.@score/(s.outdegree(e_type))
    POST-ACCUM
        s.@score = (1.0 - damping) + damping * s.@received_score,
        s.@received_score = 0,
        @@max_diff += abs(s.@score - s.@score');
----

In the last line, we compute `@@max_diff` as the absolute value of the difference between the post-ACCUM score (`s.@score`) and the pre-ACCUM score (`s.@score'`).

== Set/Bag Expression and Operators

SELECT blocks take an input vertex set and perform various selection and filtering operations to produce an output set.
Therefore, _set/bag expressions_ and their operators are a useful and powerful part of the GSQL query language. A set/bag expression can use either SetAccum or BagAccum.

.EBNF
[source,ebnf]
----
setBagExpr := name
        | globalAccumName
    	  | name "." name
		    | name "." localAccumName
		    | name "." localAccumName ("." name "(" [argList] ")")+
		    | name "." name "(" [argList] ")" [ "." FILTER "(" condition ")" ]
		    | globalAccumName ("." name "(" [argList] ")")+
		    | setBagExpr (UNION | INTERSECT | MINUS) setBagExpr
		    | "(" argList ")"
		    | "(" setBagExpr ")"
----



=== Set/Bag expression operators - UNION, INTERSECT, MINUS

The operators are straightforward, when two operands are both sets, the result expression is a set.
When at least one operand is a bag, the result expression is a bag.
If one operand is a bag and the other is a set, the operator treats the set operand as a bag containing one of each value.

==== Set/bag operator examples
[tabs]
====
Query::
+
--
.Set/Bag Operator Examples
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_set_bag_operator_examples.gsql[]
----
--

Results::
+
--
[source,gsql]
----
include::appendix:example$minimal_net/operators_expressions_set_bag_operator_examples.gsql[]
----
--
====


The result of these operators is another set or bag, so these operations can be nested and chained to form more complex expressions, such as

[source,text]
----
(setBagExpr_A INTERSECT (setBagExpr_B UNION setBagExpr_C) ) MINUS setBagExpr_D
----

=== Set/Bag Expression Membership Operators

For example , suppose setBagExpr_A is ("a", "b", "c")

[source,text]
----
"a" IN setBagExpr_A            => true
"d" IN setBagExpr_A            => false
"a" NOT IN setBagExpr_A        => false
"d" NOT IN setBagExpr_A        => true
----

The `IN` and `NOT IN` operators support all base types on the left-hand side, and any set/bag expression on the right-hand side. The base type must be the same as the accumulator's element type. `IN` and `NOT IN` return a `BOOL` value.

The following example uses `NOT IN` to exclude neighbors that are on a blocked list.

==== Example
[tabs]
====
Query::
+
--
.Set Membership example
[source,gsql]
----
include::appendix:example$social_net/operators_expressions_set_membership.gsql[]
----
--

Result::
+
--
.Results for Query `friends_not_in_blocked_list`
[source,gsql]
----
GSQL > RUN QUERY friends_not_in_blocked_list("person1", ["person2"])
include::appendix:example$social_net/operators_expressions_set_membership_results.json[]
----
--
====


[#_subqueries]
== Subqueries

A query defined with a `RETURNS` header following its `CREATE` statement is called a subquery.
Subqueries act as callable functions in GSQL: they take parameters, perform a set of actions and return a value.


A subquery must end with a xref:querying:output-statements-and-file-objects.adoc[return statement] to pass its output value to a query.
Exactly one type is allowed in the `RETURNS` header, and thus the `RETURN` statement can only return one expression.

A subquery must be created before the query that calls the subquery.
A subquery must be installed either before or in the same `INSTALL QUERY` command with the query that calls the subquery.

[CAUTION]
====
* A xref:distributed-query-mode.adoc[distributed query] cannot call another distributed query
* A non-distributed query cannot call a distributed query in an `ACCUM` or `POST-ACCUM` clause
====

.Main Components of a Subquery
[source,gsql]
----
CREATE QUERY <query_name>() FOR GRAPH <Graph_Name> <1>
RETURNS (INT) <2>
{
    // ...
    // Query body goes here
    // ...
    RETURN <return_value> <3>
}
----
<1> Parameters are optional.
<2> A subquery has a `RETURNS` header specifying its return type.
<3> The return statement of a subquery.
Return value must be the same type as specified in the `RETURNS` header.



=== Parameter types

A subquery parameter can only be one of the following types:

* Primitives: `INT`, `UINT`, `FLOAT`, `DOUBLE`, `STRING`, `BOOL`
* `VERTEX`
* A set or bag of primitive or `VERTEX` elements

=== Return types

A subquery's return value can be any xref:declaration-and-assignment-statements.adoc#_base_type_variables[base type variable] or accumulator with the following exceptions:

* If the return type is a `HeapAccum` or `GroupByAccum` that has a user-defined tuple as an element, the user-defined tuple must be xref:ddl-and-loading:defining-a-graph-schema.adoc#_typedef[defined at the catalog level].
* If the return type is a `BagAccum`, `SetAccum`, or `ListAccum` with a tuple as its element, the tuple does not need to be defined at the catalog level and can be anonymous.

==== Example

The following query uses a subquery that returns an anonymous tuple:

[tabs]
====
Subquery::
+
--
[,gsql]
----
CREATE QUERY sub_query(VERTEX x)
RETURNS (ListAccum<TUPLE<INT, STRING, DOUBLE>>)
{
    TYPEDEF tuple<INT a, STRING b, DOUBLE c> My_Tuple;
    ListAccum<My_Tuple> @@res;
    RETURN @@res;
}
----
--
Query::
+
--
[.wrap,gsql]
----
CREATE QUERY main_query() {
    TYPEDEF tuple<INT a, STRING b, DOUBLE c> My_Tuple1;
    ListAccum<My_Tuple1> @@acc1;
    V = SELECT src FROM xxxx:src
        // Put the elements returned from the subQuery at the end of the @@Acc1
        ACCUM @@Acc1 += sub_query(src);
    PRINT @@acc1;
}
----
--
====

=== Recursive subqueries

Recursion is supported for subqueries and a subquery can call itself.
The following example subquery takes a set of persons as starting points, and returns all the friends within a given distance.

While recursive subqueries may look simpler in writing, they are usually not as efficient as iterative subqueries in GSQL.


==== Example
[tabs]
====
Subquery::
+
--
[source,gsql]
----
include::appendix:example$friend_net/operators_expressions_recursive_subqueries.gsql[]
----
--
Query::
+
--
[source, gsql]
----
include::appendix:example$friend_net/operators_expressions_recursive_subqueries_main.gsql[]
----
--

Result::
+
--
Test cases: Starting from `person1`, search to a distance of `1` and a distance of `2`.

[source,gsql]
----
include::appendix:example$friend_net/operators_expressions_recursive_subqueries_run_1.gsql[]
include::appendix:example$friend_net/operators_expressions_recursive_subqueries_results_1.json[]

include::appendix:example$friend_net/operators_expressions_recursive_subqueries_run_2.gsql[]
include::appendix:example$friend_net/operators_expressions_recursive_subqueries_results_2.json[]
----
--
====

== Examples of Expressions

Below is a list of examples of expressions.
Note that `( argList )` is a set/bag expression, while `[ argList ]` is a list expression.

[tabs]
====
Query::
+
--
.Expression Examples
[source,gsql]
----
include::appendix:example$work_net/operators_expressions_expression_example.gsql[]
----
--


Results::
+
--
.expressionEx.json Results
[source,json]
----
include::appendix:example$work_net/operators_expressions_expression_example_results.json[]
----
--
====



== Examples of Expression Statements

[tabs]
====
Query::
+
--
.Expression Statement Examples
[source,gsql]
----
include::appendix:example$work_net/operators_expressions_expression_statement.gsql[]
----
--

Results::
+
--
[source,gsql]
----
GSQL > RUN QUERY expression_stmnt_ex()
include::appendix:example$work_net/operators_expressions_expression_statement_results.json[]
----
--
====